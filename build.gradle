import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.diffplug.gradle.spotless" version "4.3.0"
    id 'org.springframework.boot' version '2.3.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
}

group = 'edu.oakland'
version = '0.0.1'
sourceCompatibility = '1.8'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

spotless {
  java {
    googleJavaFormat()
    importOrder 'edu', 'java', 'com org', ''
    removeUnusedImports()
    trimTrailingWhitespace()
  }
}

processResources {
  filesMatching("**/application.properties") {
    def props = new Properties()

	file(System.getenv("PORTAL_HOME") + "/global.properties").withInputStream {
		props.load(it)
	}

	file(System.getenv("PORTAL_HOME") + "/uPortal.properties").withInputStream {
		props.load(it)
	}

	filter(ReplaceTokens, tokens: props)

  }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

clean.finalizedBy("spotlessApply")

test {
    useJUnitPlatform()
}
